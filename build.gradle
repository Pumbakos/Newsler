plugins {
    id 'java'
    id 'jacoco'
    id 'org.springframework.boot' version '3.0.1'
    id 'io.spring.dependency-management' version '1.1.0'
}

group = 'pl.newsler'
version = '0.0.1'
sourceCompatibility = '17'

ext {
    maildevContainerName = 'newsler.maildev'
    smtpMailDevPort = '1025:1025'
    browserMailDevPort = '1080:1080'
}

jar {
    archiveFileName = 'newsler.jar'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    google()
}

jacoco {
    toolVersion = "0.8.8"
    reportsDirectory = layout.buildDirectory.dir('test-coverage-results') as Directory
}

dependencies {
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor:3.0.1'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'

    compileOnly 'org.projectlombok:lombok:1.18.24'

    implementation 'org.springframework.boot:spring-boot-starter-validation:3.0.1'
    implementation 'org.springframework.boot:spring-boot-starter-data-rest:3.0.1'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa:3.0.1'
    implementation 'org.springframework.boot:spring-boot-starter-webflux:3.0.1'
    implementation 'org.springframework.boot:spring-boot-starter-jersey:3.0.1'
    implementation 'org.springframework.boot:spring-boot-starter-mail:3.0.1'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc:3.0.1'
    implementation 'org.springframework.boot:spring-boot-starter-web:3.0.1'

    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf:3.0.1'
    implementation 'org.springframework.boot:spring-boot-starter-security:3.0.1'

    implementation('com.github.javafaker:javafaker:1.0.2') { exclude module: 'snakeyaml' }
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.4.2'
    implementation 'net.htmlparser.jericho:jericho-html:3.4'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'org.jetbrains:annotations:23.1.0'
    implementation 'com.h2database:h2:2.1.214'
    implementation 'com.auth0:java-jwt:4.2.1'
    implementation 'org.yaml:snakeyaml:1.33'

    runtimeOnly 'com.oracle.database.jdbc:ojdbc8:21.8.0.0'
    runtimeOnly 'com.h2database:h2:2.1.214'

    testAnnotationProcessor 'org.projectlombok:lombok:1.18.24'

    testCompileOnly 'org.projectlombok:lombok:1.18.24'

    testImplementation('com.github.javafaker:javafaker:1.0.2') { exclude module: 'snakeyaml' }
    testImplementation 'org.springframework.boot:spring-boot-starter-test:2.7.5'
    testImplementation 'org.springframework.security:spring-security-test:5.7.5'
    testImplementation 'com.squareup.retrofit2:converter-gson:2.1.0'
    testImplementation 'uk.org.webcompere:system-stubs-core:2.0.1'
    testImplementation 'com.squareup.retrofit2:retrofit:2.9.0'
    testImplementation 'org.mockito:mockito-inline:3.12.4'
    testImplementation 'org.projectlombok:lombok:1.18.24'
    testImplementation 'com.google.code.gson:gson:2.10'
    testImplementation 'org.yaml:snakeyaml:1.33'
    testImplementation 'junit:junit:4.13.1'
}

jacocoTestReport {
    reports {
        xml.required = false
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir('test-coverage-results/html')
    }
    finalizedBy jacocoTestCoverageVerification
}

jacocoTestCoverageVerification {
    dependsOn('jacocoTestReport')
    violationRules {
        rule {
            limit {
                minimum = 0.70
            }
        }
    }
}

tasks.register('runMailDevContainer', Exec) {
    group 'newsler'
    commandLine 'docker', 'run', '-p', project.ext.browserMailDevPort, '-p', project.ext.smtpMailDevPort,
            '--name', project.ext.maildevContainerName, 'maildev/maildev', '--detach', 'newsler-maildev'
}

tasks.register('stopMailDevContainer', Exec) {
    group 'newsler'
    commandLine 'docker', 'stop', project.ext.maildevContainerName
}

tasks.register('deleteMailDevContainer', Exec) {
    group 'newsler'

    dependsOn(tasks.named('stopMailDevContainer').name)
    commandLine 'docker', 'rm', project.ext.maildevContainerName
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}